---
order: 0
---

# TCP (and TLS)

MQTT is a IoT friendly protocol for publishing and subscribing to a shared MQTT broker, from a wide range of devices. While Toits pub-sub implementation has some benefits for seamlessly storing and offloading messages, there may be usecases where connecting to an MQTT broker can be beneficial. For example if

1. You want to send the data to a local machine and not through the internet.
2. Your stack already operates using an existing MQTT broker.
3. You want to quickly evalute Toit.

Either way, this guide shows you how to connect to a MQTT broker using TCP (or TLS).

## Connecting

MQTT requires every client to have an unique `CLIENT_ID`. That ID is send when connecting to the broker and helps the broker to ensures
easy client will get a copy of data broadcasted, etc.

Next, some brokers requires the client to provide a `username` and `password`.

The Toit MQTT client is available from the [`mqtt` package](https://pkg.toit.io/package/github.com%2Ftoitware%2Fmqtt).

```toit
import net
import mqtt

CLIENT_ID ::= "my-client-id"
HOST      ::= "localhost"
PORT      ::= 1883

main:
  socket := net.open.tcp_connect HOST PORT
  client := mqtt.Client
    CLIENT_ID
    mqtt.TcpTransport socket

  // Client is now connected.
```

## Publish

Simple sensor applications mainly have one purpose; send sensor data. When sending data, you simply need to tell which topic to publish to, along with the payload.


```toit
import mqtt
import encoding.json

publish value/float client/mqtt.Client:
  payload := json.encode {
    "value": value
  }
  client.publish "my/sensor/data" payload

```

## Subscribe

Subscribing to data requires two steps. First, you have to set up the callback to handle the incoming messages. Second, you have to register to the topics you want data from.

```toit
import mqtt
import encoding.json

subscribe client/mqtt.Client:
  client.subscribe "my/sensor/*" --qos=1

  client.handle: | topic/string payload/ByteArray |
    decoded := json.decode payload
    print "Received value on '$topic': $(decoded["value"])"
```

## TLS

The `TcpTransport` used so far works with any TCP-compatible socket, including TLS sockets. To upgrade a TCP socket to TLS, you need to perform
a TLS handshake.


```toit
import mqtt
import net
import tls
import net.x509

CLIENT_ID ::= "my-client-id"
HOST      ::= "localhost"
PORT      ::= 8883

main:
  socket := net.open.tcp_connect HOST PORT

  tls_socket := tls.Socket.client socket
    --server_name=HOST
    --root_certificates=[SERVER_CERT]

  tls_socket.handshake

  client := mqtt.Client
    CLIENT_ID
    mqtt.TcpTransport tls_socket

  // Client is now connected.

SERVER_CERT := x509.Certificate.parse """\
-----BEGIN CERTIFICATE-----

<- insert server cert ->

-----END CERTIFICATE-----
"""
```
