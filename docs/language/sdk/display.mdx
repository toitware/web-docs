---
order: 3
---

# Display

The Toit SDK includes support for writing text and printing icons on displays.
We highlight in this section the basic functions of display use. You can also learn more about the different fonts available in the SDK by reading [Toit font basics](../fonts).

These examples assume you have installed the [pixel_display](https://github.com/toitware/toit-pixel-display) package into
your project.  See the [Packages quick start guide](../../package/pkgguide)
for information on getting started with packages.

You will also need at least one display driver, for example the [SSD1306 driver](https://github.com/toitware/toit-ssd1306)
which is also available as a package, called `ssd1306`.

## Getting the display driver and PixelDisplay object

Both the `pixel_display` and `ssd1306` packages have some helpful examples which you can see by cloning the
github repos linked above.  We will assume that you have a `get_display.toit` file in your project, which
might look something like the following.  Details will depend on your hardware configuration, in particular which
pins and bus you are using to connect your display.

Example contents of `get_display.toit`

```
import gpio
import i2c
import ssd1306 show *
import pixel_display show *

get_display -> TwoColorPixelDisplay:
  scl := gpio.Pin 4
  sda := gpio.Pin 5
  bus := i2c.Bus
    --sda=sda
    --scl=scl
    --frequency=800_000

  devices := bus.scan
  if not devices.contains SSD1306_ID: throw "No SSD1306 display found"

  driver := SSD1306 (bus.device 0x3c)

  return TwoColorPixelDisplay driver
```

## Write with built-in sans font

```
import font show *
import pixel_display.texture show *
import pixel_display.two_color show BLACK
import pixel_display show *

import .get_display  // Import file in current project, see above.

sans ::= Font.get "sans10"
display ::= get_display

main:
  context := display.context --landscape --color=BLACK --font=sans
  display.text context 60 50 "Hello from Toit!"
  display.draw
```

## Import additional font

Import specific fonts from `font.x11_100dpi` with:

```
import font show *
import font.x11_100dpi.sans.sans_10_bold as sans_10_bold
import font.x11_100dpi.sans.sans_24_bold as sans_24_bold
import pixel_display.texture show*
import pixel_display.two_color show WHITE BLACK
import pixel_display show TwoColorPixelDisplay

import .get_display  // Import file in current project, see above.

main:
  sans_10_bold ::= Font [sans_10_bold.ASCII, sans_10_bold.LATIN_1_SUPPLEMENT]
  sans_24_font ::= Font [sans_24_bold.ASCII, sans_24_bold.LATIN_1_SUPPLEMENT]
  display ::= get_display

  sans_10 := display.context
    --landscape
    --font = sans_10_bold
    --color = BLACK
    --alignment = TEXT_TEXTURE_ALIGN_RIGHT

  sans_24 := display.context
    --landscape
    --font = sans_24_font
    --color = BLACK
    --alignment = TEXT_TEXTURE_ALIGN_CENTER

  context := display.context --landscape --color=BLACK
  display.text sans_10 40 30 "Hello from"
  display.text sans_24 18 65 "TOITWARE"
  display.draw
```

Learn more about all available fonts [here](../fonts).

## Add icons

All [Material Design](https://materialdesignicons.com/) icons are available for use in Toit programs.

```
import pixel_display show TwoColorPixelDisplay
// Use "toit pkg install pictogrammers_icons" to get this package.
import pictogrammers_icons.size_96 as icons

import .get_display  // Import file in current project, see above.

main:
  display := get_display
  context := display.context --landscape
  icon := display.icon context 63 85 icons.HUMAN_SCOOTER
  display.draw
```
